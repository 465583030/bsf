#include "$ENGINE$\PerObjectData.bslinc"

#include "$ENGINE$\SkinnedVertexInput.bslinc"
#include "$ENGINE$\NormalVertexInput.bslinc"
#define USE_BLEND_SHAPES
#include "$ENGINE$\SkinnedVertexInput.bslinc"
#include "$ENGINE$\NormalVertexInput.bslinc"
#undef USE_BLEND_SHAPES

mixin ShadowDepthBase
{
	code
	{
		struct ShadowVStoFS
		{
			float4 position : SV_Position;
			
			#ifdef USES_GS
			float4 worldPos : TEXCOORD0;
			#endif
		};
		
		cbuffer ShadowParams
		{
			float4x4 gMatViewProj;
			float gDepthBias;
			float gDepthRange;
		};

		void linearizeDepth(inout float4 clipPos)
		{
			float ndcZ = clipPos.z / clipPos.w;
			float deviceZ = NDCZToDeviceZ(ndcZ);
			
			#ifdef CLAMP_TO_NEAR_PLANE
			// Clamp to near plane if behind it
			if (deviceZ < 0)
			{
				clipPos.z = DeviceZToNDCZ(0);
				clipPos.w = 1.0f;
			}
			#endif

			// Output linear depth
			#ifdef LINEAR_DEPTH_RANGE
				float linearDepth = deviceZ * gDepthRange + gDepthBias;
				clipPos.z = DeviceZToNDCZ(linearDepth) * clipPos.w;
			#endif
		}		
	
		ShadowVStoFS vsmain(VertexInput_PO input)
		{
			ShadowVStoFS output;
		
			float4 worldPosition = getVertexWorldPosition(input);
			
			#ifdef USES_GS
			output.worldPos = worldPosition;
			output.position = worldPosition;
			#else
			float4 clipPos = mul(gMatViewProj, worldPosition);
			linearizeDepth(clipPos);
			
			output.position = clipPos;
			#endif
			
			return output;
		}
	};
};

technique ShadowDepth
{
	mixin PerObjectData;
	mixin NormalVertexInput;
	mixin ShadowDepthBase;
	mixin ShadowDepth;
};

technique ShadowDepthSkinned
{
	mixin PerObjectData;
	mixin SkinnedVertexInput;
	mixin ShadowDepthBase;
	mixin ShadowDepth;

	tags = { "Skinned" };
};

technique ShadowDepthMorph
{
	mixin PerObjectData;
	mixin MorphVertexInput;
	mixin ShadowDepthBase;
	mixin ShadowDepth;

	tags = { "Morph" };
};

technique ShadowDepthSkinnedMorph
{
	mixin PerObjectData;
	mixin SkinnedMorphVertexInput;
	mixin ShadowDepthBase;
	mixin ShadowDepth;

	tags = { "SkinnedMorph" };
};